<?xml version='1.0'?>

<effect>
	<include name="PostProcess.fxml"/>
	<include name="util.fxml"/>
	
	<macro name="RGB_TO_LUM" value="float3(0.2126f, 0.7152f, 0.0722f)"/>

	<parameter type="sampler" name="linear_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<parameter type="sampler" name="point_sampler">
		<state name="filtering" value="min_mag_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<parameter type="texture2D" name="src_tex"/>
	<parameter type="texture2D" name="lum_tex"/>
	<parameter type="texture2D" name="bloom_tex"/>

	<shader>
		<![CDATA[
float EyeAdaption(float lum)
{
	return lerp(0.2f, lum, 0.5f);
}

float3 ACESFilm(float3 x)
{
	const float A = 2.51f;
	const float B = 0.03f;
	const float C = 2.43f;
	const float D = 0.59f;
	const float E = 0.14f;

	return (x * (A * x + B)) / (x * (C * x + D) + E);
}

float3 ToneMapping(float3 color, float3 blur, float adapted_lum)
{
	const float3 BLUE_SHIFT = float3(0.4f, 0.4f, 0.7f);

	color += blur * 0.25f;
	
	float lum = dot(color, RGB_TO_LUM);
	
	// martin's modified blue shift
	color = lerp(lum * BLUE_SHIFT, color, saturate(16.0f * lum));

#if KLAYGE_NO_FP_COLOR
	return color;
#else
	float adapted_lum_dest = 2 / (max(0.1f, 1 + 10 * EyeAdaption(adapted_lum)));

	return ACESFilm(adapted_lum_dest * color);
#endif
}

float4 ToneMapping20PS(float2 iTex : TEXCOORD0) : SV_Target
{
#if KLAYGE_NO_FP_COLOR
	float adapted_lum = 1;
#else
	float adapted_lum = ReadAFloat(lum_tex.Sample(point_sampler, 0.5f.xx), 16);
#endif
	float lum = max(0.001f, adapted_lum);
	// For FXAA
	float3 ldr_rgb = saturate(ToneMapping(src_tex.Sample(linear_sampler, iTex).rgb, bloom_tex.Sample(linear_sampler, iTex).rgb, lum));
	return float4(ldr_rgb, dot(ldr_rgb, RGB_TO_LUM));
}
		]]>
	</shader>

	<shader version="3">
		<![CDATA[		
void ToneMapping30VS(float4 pos : POSITION,
					out float3 oTex : TEXCOORD0,
					out float4 oPos : SV_Position)
{
	oPos = pos;

	oTex.xy = TexCoordFromPos(pos);
#if KLAYGE_NO_FP_COLOR
	float adapted_lum = 1;
#else
	float adapted_lum = ReadAFloat(lum_tex.SampleLevel(point_sampler, 0.5f.xx, 0), 16);
#endif
	oTex.z = max(0.001f, adapted_lum);
}

float4 ToneMapping30PS(float3 iTex : TEXCOORD0) : SV_Target
{
	// For FXAA
	float3 ldr_rgb = saturate(ToneMapping(src_tex.Sample(linear_sampler, iTex.xy).rgb, bloom_tex.Sample(linear_sampler, iTex.xy).rgb, iTex.z));
	return float4(ldr_rgb, dot(ldr_rgb, RGB_TO_LUM));
}
		]]>
	</shader>

	<technique name="ToneMapping20">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="ToneMapping20PS()"/>
		</pass>
	</technique>

	<technique name="ToneMapping30">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="ToneMapping30VS()"/>
			<state name="pixel_shader" value="ToneMapping30PS()"/>
		</pass>
	</technique>
</effect>
