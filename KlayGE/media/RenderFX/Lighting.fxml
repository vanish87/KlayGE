<?xml version='1.0'?>

<effect>
	<macro name="MAX_SHININESS" value="8192.0f"/>

	<shader>
		<![CDATA[
float DirectionalLighting(float3 light_dir, float3 normal)
{
	return dot(-light_dir, normal);
}

float PointLighting(float3 light_pos, float3 pos, float3 normal)
{
	return dot(normalize(light_pos - pos), normal);
}

float SpotLighting(float3 light_pos, float3 light_dir, float2 cos_cone, float3 pos)
{
	// cos_cone is (cos_outer_cone, cos_inner_cone)
	
	float3 v = normalize(pos - light_pos);
	float cos_direction = dot(v, light_dir);

	return smoothstep(cos_cone.x, cos_cone.y, cos_direction);
}


float3 FresnelTerm(float3 light_vec, float3 halfway_vec, float3 c_spec)
{
	float e_n = saturate(dot(light_vec, halfway_vec));
	return c_spec > 0 ? c_spec + (1 - c_spec) * exp2(-(5.55473f * e_n + 6.98316f) * e_n) : 0;
}

float SpecularNormalizeFactor(float shininess)
{
	return (shininess + 2) / 8;
}

float3 DistributionTerm(float3 halfway_vec, float3 normal, float shininess)
{
	return exp((shininess + 0.775f) * (max(dot(halfway_vec, normal), 0.0f) - 1));
}

float3 SpecularTerm(float3 c_spec, float3 light_vec, float3 halfway_vec, float3 normal, float shininess)
{
	return SpecularNormalizeFactor(shininess) * DistributionTerm(halfway_vec, normal, shininess)
		* FresnelTerm(light_vec, halfway_vec, c_spec);
}


// diffuse only
float3 CalcBRDFShading(float3 c_diff, float3 l, float3 n)
{
	return max(c_diff * dot(n, l), 0);
}
// diffuse + specular
float3 CalcBRDFShading(float3 c_diff, float3 c_spec, float shininess, float3 l, float3 h, float3 n)
{
	return max((c_diff + SpecularTerm(c_spec, l, h, n, shininess)) * dot(n, l), 0);
}


float AttenuationTerm(float3 light_pos, float3 pos, float3 atten)
{
	float3 v = light_pos - pos;
	float d2 = dot(v, v);
	float d = sqrt(d2);
	return 1 / dot(atten, float3(1, d, d2));
}

float3 CalcShading(float3 diff_lighting, float3 spec_lighting, float shininess,
				float3 diffuse, float3 specular, float3 view_dir, float3 normal)
{
	return float3(max(diff_lighting * diffuse
		+ SpecularNormalizeFactor(shininess) * spec_lighting
			* FresnelTerm(normalize(view_dir), normal, specular), 0));
}

float3 CalcShading(float4 lighting, float shininess, float3 diffuse, float3 specular,
				float3 view_dir, float3 normal)
{
	const float3 RGB_TO_LUM = float3(0.2126f, 0.7152f, 0.0722f);
	float3 diff_lighting = lighting.rgb;
	float3 spec_lighting = lighting.a / (dot(lighting.rgb, RGB_TO_LUM) + 1e-6f) * lighting.rgb;
	return CalcShading(diff_lighting, spec_lighting, shininess, diffuse, specular,
		view_dir, normal);
}


// Environment BRDF

float CalcPrefilteredEnvMip(float glossiness, int num_mip)
{
	return num_mip - glossiness * num_mip;
}

float3 CalcPrefilteredEnvVec(float3 normal, float3 view)
{
	return -reflect(view, normal);
}

float3 CalcEnvDiffuse(float3 prefiltered_env, float3 c_diff)
{
	return prefiltered_env * c_diff;
}

float3 CalcEnvSpecular(float3 prefiltered_env, float3 c_spec, float glossiness, float3 normal, float3 view)
{
	float n_dot_v = saturate(dot(normal, view));
	glossiness = max(0.5f / 16, glossiness);
	float2 env_brdf;
	float4 tmp = ((float4(-4.996914762f, 7.253111161f, -1.963867075f, -0.170416225f) * glossiness
		+ float4(8.993475061f, -15.42473953f, 4.712593001f, 0.448102365f)) * glossiness
		+ float4(-4.928234727f, 10.95286522f, -3.852980973f, -0.389332014f)) * glossiness
		+ float4(0.846124834f, -1.596815751f, 1.005729748f, 0.113484128f);
	env_brdf.x = (((tmp.x * n_dot_v + tmp.y) * n_dot_v + tmp.z) * n_dot_v) + tmp.w;
	tmp = ((float4(-0.684077741f, 1.316163916f, -0.70869252f, 0.073264505f) * glossiness
		+ float4(0.719315865f, -1.384932814f, 0.775880046f, -0.105818706f)) * glossiness
		+ float4(-0.101034049f, 0.18680998f, -0.117919199f, 0.030727381f)) * glossiness
		+ float4(0.001922126f, 0.006697305f, -0.015741592f, 0.007229544f);
	env_brdf.y = (((tmp.x * n_dot_v + tmp.y) * n_dot_v + tmp.z) * n_dot_v) + tmp.w;
	env_brdf = saturate(env_brdf);
	return prefiltered_env * (c_spec * env_brdf.x + env_brdf.y);
}

// Area lighting

float AreaLightShininessFixup(float shininess, float3 light_pos, float light_radius, float3 point_pos)
{
	return shininess + light_radius / (2 * length(light_pos - point_pos));
}

float3 SphereAreaLightPositionFixup(float3 light_pos, float light_radius, float3 point_pos,
		float3 normal, float3 view_dir)
{
	float3 l = light_pos - point_pos;
	float3 r = reflect(view_dir, normal);
	float3 intersect_point = dot(l, r) * r;
	float3 center_to_ray = intersect_point - l;
	return light_pos + center_to_ray * saturate(light_radius / length(center_to_ray));
}

float3 TubeAreaLightPositionFixup(float3 l0, float3 l1, float3 point_pos, float3 normal, float3 view_dir)
{
	float3 ld = l1 - l0;
	float3 r = reflect(view_dir, normal);
	float r_dot_ld = dot(r, ld);
	float t = (dot(r, l0) * dot(r, ld) - dot(l0, ld)) / (dot(ld, ld) - r_dot_ld * r_dot_ld);
	return l0 + point_pos + saturate(t) * ld;
}

float2 TubeAreaLightAttenuationIrradianceFixup(float3 l0, float3 l1, float3 normal)
{
	float len_l0 = length(l0);
	float len_l1 = length(l1);
	return float2(1.0f / (len_l0 * len_l1 + dot(l0, l1)),
		dot(normal, l0) / len_l0 + dot(normal, l1) / len_l1);
}

// Metalness

float3 DiffuseColor(float3 albedo, float metalness)
{
	return albedo * (1 - metalness);
}

float3 SpecularColor(float3 albedo, float metalness)
{
	return lerp(0.04, albedo, metalness);
}

// Glossiness

float Shininess2Glossiness(float shininess)
{
	return log2(shininess) / log2(MAX_SHININESS);
}

float Glossiness2Shininess(float glossiness)
{
	return pow(MAX_SHININESS, glossiness);
}
		]]>
	</shader>
</effect>
